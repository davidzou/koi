import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens

group 'com.wonderingwall.koi'
version '1.0.0-SNAPSHOT'

apply plugin: 'groovy'

sourceCompatibility = 1.8

[compileJava,compileTestJava,javadoc]*.options*.encoding = 'UTF-8'

archivesBaseName="koi"
// 这里不用管，build.sh里面自动替换配置
version="1.0.3"

jar {
    manifest {
        attributes 'Implementation-Title': 'koi what a gradle plugin project creator.',
                'Implementation-Version': version,
                'Main-Class': 'com.wonderingwall.koi.Kohaku'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    compile 'commons-cli:commons-cli:1.4'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// 项目打包执行的时间获取
def projectBuildDate = new Date().format("yyyy.MM.dd")
def author = "DavidZou (wearecisco@gmail.com)"

task distTar(dependsOn: 'jar') {
    doLast {
        copy {
            logger.quiet("Copy exec shell deploy to build dir")
            from ("src/shell/") {
                include "koi.sh"
                rename { fileName ->
                    fileName.replace('.sh', '')
                }
                filter(FixCrLfFilter)
                filter(ReplaceTokens, tokens: [date: projectBuildDate, version: version, author: author])
                filteringCharset = 'UTF-8'
            }
            into "$project.buildDir"
        }

        mkdir ("tarball/${version}")
        copy {
            logger.quiet("Copy jar to tarball")
            from ("$project.buildDir/libs") {
                include "*.jar"
            }
            into "tarball/${version}"
        }
        exec {
            commandLine 'tar', "-C", "build/", '-cvf', "tarball/${version}/${archivesBaseName}-${version}.tar", "libs/", "koi"
        }
        exec {
            commandLine 'tar', "-C", "build/", '-zcvf', "tarball/${version}/${archivesBaseName}-${version}.tar.gz", "libs/", "koi"
        }
    }
}
